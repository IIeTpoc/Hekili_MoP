# Warlock: Destruction - WoWSims
## Prepull
; removed jade serpent potion usage
actions.precombat+=/incinerate,if=time=-1.75

## Default
actions+=/call_action_list,name=variables
actions+=/call_action_list,name=ogcd
actions+=/run_action_list,name=opener,strict=1,if=time<=5
actions+=/run_action_list,name=move,if=moving
actions+=/call_action_list,name=cleave,if=active_enemies=2
actions+=/call_action_list,name=aoe,if=active_enemies>2
actions+=/call_action_list,name=st

## Variables
actions.variables+=/variable,name=execute_phase,op=set,value=target.health.pct<20
actions.variables+=/variable,name=cds_active,op=set,value=buff.dark_soul.up|trinket.stat.any.up

## OGCDs
actions.ogcd+=/dark_soul,if=(cooldown.dark_soul.ready&fight_remains<=22)|(spell.dark_soul.charges>0&spell.dark_soul.time_to_next_charge<=57&fight_remains<=77)
actions.ogcd+=/berserking,if=variable.cds_active
actions.ogcd+=/blood_fury,if=variable.cds_active
actions.ogcd+=/arcane_torrent,if=mana.pct<80&burning_embers<4
actions.ogcd+=/lifeblood,if=variable.cds_active
actions.ogcd+=/use_item,slot=hands,if=cooldown.synapse_springs.ready&variable.cds_active
actions.ogcd+=/summon_terrorguard,if=fight_remains<=61

## Opener
actions.opener+=/immolate,if=!ticking
actions.opener+=/conflagrate,if=charges=2

## Movement
actions.move+=/fel_flame,if=!talent.kiljaedens_cunning.enabled
actions.move+=/incinerate,if=talent.kiljaedens_cunning.enabled

## Cleave (2 targets)
actions.cleave+=/havoc,target=2
actions.cleave+=/shadowburn,if=variable.execute_phase
actions.cleave+=/chaos_bolt,if=buff.backdraft.stack<3&burning_embers>=1
actions.cleave+=/immolate,cycle_targets=1,if=remains<=tick_time
actions.cleave+=/conflagrate
actions.cleave+=/incinerate

## AoE (3+ targets)
actions.aoe+=/rain_of_fire,if=!ticking
actions.aoe+=/fire_and_brimstone,if=burning_embers>=2
actions.aoe+=/immolate,cycle_targets=1,max_cycle_targets=5,if=remains<=tick_time
actions.aoe+=/conflagrate
actions.aoe+=/incinerate

## Single Target
actions.st+=/immolate,cycle_targets=1,max_cycle_targets=3,if=remains<=tick_time
actions.st+=/immolate,if=remains<=gcd|remains<=7.5&buff.dark_soul.up
actions.st+=/shadowburn,if=variable.execute_phase
actions.st+=/chaos_bolt,if=burning_embers>=1&buff.backdraft.stack<3&(trinket.stat.any.icd_ready|buff.dark_soul.up)
actions.st+=/chaos_bolt,if=burning_embers>=2&buff.backdraft.stack<3
actions.st+=/chaos_bolt,if=burning_embers.deficit<0.5&buff.backdraft.stack<3
actions.st+=/chaos_bolt,if=target.time_to_die<15&burning_embers>=1&buff.backdraft.stack<3
actions.st+=/conflagrate,if=charges=2
actions.st+=/immolate,if=remains<=tick_time
actions.st+=/conflagrate
actions.st+=/incinerate
